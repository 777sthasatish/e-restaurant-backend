buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
	apply from: "dependencies.gradle"
}

group = 'com.cotiviti.e-restaurant-backend'
version = '0.0.1-SNAPSHOT'

allprojects {
	apply plugin: "io.spring.dependency-management"
	apply plugin: "java"

	sourceCompatibility = 11
	targetCompatibility = 11

	repositories {
		maven {
			url "https://gitlab.com/api/v4/groups/10170188/-/packages/maven"
			name "GitLab"
			credentials(HttpHeaderCredentials) {
				name = 'Private-Token'
				value = gitLabPrivateToken
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
		jcenter()
		mavenCentral()
	}

	dependencies {
		implementation bootStarter
		testImplementation testing.bootStarter
	}

	test {
		useJUnitPlatform()
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR9")
		}
		imports {mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")}
	}
}


subprojects {

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation (
				developerTool.mapStruct,
				local.satishCommon
		)
		compileOnly developerTool.lombok
		annotationProcessor (
				developerTool.lombok,
				developerTool.mapStructProcessor
		)
	}
}


dependencies {
	implementation(
			project(":domain"),
			project(":driven-adapter"),
			project(":driver-adapter")
	)
}